 NAM LISP-SUBRS1
 OPT PAG
 PAG
OBEGADD EQU $0BB0
CAR EQU 0
CDR EQU 2
EOI EQU 4
*
 LIB LISPLB
* GLOBAL VECTORS
 ORG $0100
EVAL RMB 3
READ RMB 3
PRINT RMB 3
 RMB 3
GETCEL RMB 3
FRECEL RMB 3
PUSHX RMB 3
POPX RMB 3
TOPX RMB 3
EVLATM RMB 3
SETATM RMB 3
LOOKUP RMB 3
GNXTL RMB 3
GFNXTL RMB 3
LSTINI RMB 3
LSTADD RMB 3
LSTAD2 RMB 3
LSTEND RMB 3
LSTEN0 EQU POPX
ERREX RMB 3
 JMP ATMINI
ISDTPR RMB 3
ISATOM RMB 3
GETC RMB 3
NUMINV RMB 3
NUMFRM RMB 3
FRMNUM RMB 3
PUTSYM RMB 3
PUTC RMB 3
ERRBRK RMB 3
GETSYM RMB 3
STKFRG RMB 3
GCOL RMB 3
PROPSH RMB 3
GETCAR RMB 3
ISVAR RMB 3
PRINR RMB 3
PROPOP RMB 3
*
 ORG OBEGAD
WARMS EQU $AD03
NOTHAT EQU FLP
*
ATMINI LDX #ATILST
 STX SYMPTR
ATI2 JSR LOOKUP
 LDX SYMPTR
ATI3 INX
 TST 0,X
 BNE ATI3
 STX SYMPTR
 LDX 0,X
 LDAA FORM
 STAA 0,X
 LDAA FORM+1
 STAA 1,X
 LDX SYMPTR
 INX
 INX
 TST 0,X
 BNE ATI2
*
 LDX #EOINAM
 JSR LOOKUP
 STX EOIATM
*
 BSR SSETAT
 LDX NOTHAT
 BSR SSETAT
*
 LDX TATOM
 STX CDR-1,X
*
 LDX #SUBRLS
 BSR SNSINI
 LDX SUBRAT
 LDX CDR-1,X
 STX FORM
 LDX NSUBAT
 BSR SSETAT
 LDX LAMATM
 BSR SSETAT
 LDX NLAMAT
 BSR SSETAT
 LDX CONATM
 BSR SSETAT
 LDX SUBLS2
 BSR SNSINI
 LDX SUBLS3
 BRA SNSINI
*
SSETAT JMP SETATM
*
SNSINI LDAB #SUBRAT
 BSR SUBINI
 INX
 LDAB #NSUBAT
*
SUBINI STX SYMPTR
 CLR XTMP
 STAB XTMP+1
 LDX XTMP
 LDX 0,X
 STX ALP
*
SBILUP LDX SYMPTR
 TST 0,X
 BEQ SBIRT
 JSR LOOKUP
 STX NLP
 LDX SYMPTR
SBILP2 INX
 TST 0,X
 BNE SBILP2
 INX
 STX SYMPTR
 LDX 0,X
 LDAA 0,X
 CMPA #'!
 BNE SBIBAD
 TST 1,X
 BNE SBIBAD
 STX FLP
 JSR GETCEL
 LDAA FLP
 STAA CAR,X
 LDAA FLP+1
 STAA CAR+1,X
 INX
 STX FORM
 JSR GETCEL
 LDAA ALP
 STAA CAR,X
 LDAA ALP+1
 STAA CAR+1,X
 LDAA FORM
 STAA CDR,X
 LDAA FORM+1
 STAA CDR+1,X
 STX FORM
 LDX NLP
 JSR SETATM
 LDX SYMPTR
 INX
 INX
 STX SYMPTR
 BRA SBILUP
*
SBIRT RTS
*
SBIBAD LDX #SBIEMS
 JMP ERREX
*
MAGWRD EQU $2100
SELFUN FDB MAGWRD
*
 LDX CUREVL
 LDX CAR,X
 RTS
*
SYSFUN FDB MAGWRD
*
 JMP WARMS
*
EVLFUN FDB MAGWRD
 BSR GCARA
 JMP EVAL
*
REDFUN FDB MAGWRD
 JMP READ
*
PRIFUN FDB MAGWRD
 BSR GCARA
 JMP PRINT
*
CARFUN FDB MAGWRD
 BSR GCARA
 JSR ISDTPR
 BEQ CARNIL
 BCS CARERR
 LDX CAR,X
 RTS
*
CARNIL LDX EOIATM
 RTS
*
CDRFUN FDB MAGWRD
 BSR GCARA
 JSR ISDTPR
 BCS CARERR
 LDX CDR,X
 RTS
*
CARERR LDX #CADEMS
 JMP ERRBRK
*
CNSFUN FDB MAGWRD
 BSR GNXTA
 STX FLP
 BSR GCARA
 STX NLP
 JSR GETCEL
 LDAA FLP
 STAA CAR,X
 LDAA FLP+1
 STAA CAR+1,X
 LDAA NLP
 STAA CDR,X
 LDAA NLP+1
 STAA CDR+1,X
 RTS
*
QUTFUN FDB MAGWRD
 BSR GCARA
 RTS
*
SETFUN FDB MAGWRD
 BSR GNXTA
 JSR ISVAR
 BCS SETERR
 STX NLP
 BSR GCARA
 STX FORM
 LDX NLP
 JSR SETATM
 LDX FORM
 RTS
*
SETERR STX FORM
 LDX #SETEMS
 JMP ERRBRK
*
GCARA LDX ALP
 JMP GETCAR
*
GNXTA LDX ALP
 JSR GNXTL
 STX ALP
 LDX XTMP
 RTS
*
EQFUN FDB MAGWRD
 BSR GNXTA
 STX FORM
 BSR GCARA
 CPX FORM
 BEQ EQYES
 LDX ZERO
 RTS
*
EQYES LDX TATOM
 RTS
*
CNDFUN FDB MAGWRD
CNDLUP BSR GNXTA
 BCS CNDRT
 JSR ISDTPR
 BCS CNDERT
 LDAA CDR,X
 STAA NLP
 LDAA CDR+1,X
 STAA NLP+1
 LDX CAR,X
 CPX TATOM
 BEQ CNDTRU
 JSR EVAL
 STX FORM
 BEQ CNDLUP
*
CNDTRU LDX NLP
 STX FORM
*
CNDTLP JSR GNXTL
 BCS CNDFRT
 STX NLP
 LDX XTMP
 JSR EVAL
 LDX NLP
 BGT CNDTLP
*
CNDFRT LDX FORM
CNDRT RTS
*
CNDERT JMP EVAL
*
PATFUN FDB MAGWRD
 BSR GCARA
 JSR ISATOM
 BCS SETERR
 BGT PATF2
*
PATF1 JMP PUTSYM
*
PATF2 STX FORM
 DEX
 LDX CAR,X
 LDAA 0,X
 CMPA #'"
 BEQ PATF3
 LDX FORM
 BRA PATF1
*
PATF3 INX
 LDAA 0,X
 BEQ PATFRT
 CMPA #'"
 BNE PATF4
 INX
 LDAA 0,X
 BEQ PATFRT
*
PATF4 JSR PUTC
 BRA PATF3
*
PATFRT LDX FORM
 RTS
*
 LIB ATILST
*
BEGADR EQU *
 END

