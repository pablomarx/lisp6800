 NAM LISP-SUBRS2
 OPT PAG
 PAG
OBEGAD EQU $0E90
CAR EQU 0
CDR EQU 2
EOI EQU 4
*
 LIB LISPLB6
* GLOBAL VECTORS
 ORG $0100
EVAL RMB 3
READ RMB 3
PRINT RMB 3
 RMB 3
GETCEL RMB 3
FRECEL RMB 3
PUSHX RMB 3
POPX RMB 3
TOPX RMB 3
EVLATM RMB 3
SETATM RMB 3
LOOKUP RMB 3
GNXTL RMB 3
GFNXTL RMB 3
LSTINI RMB 3
LSTADD RMB 3
LSTAD2 RMB 3
LSTEND RMB 3
LSTEN0 EQU POPX
ERREX RMB 3
ATMINI RMB 3
ISDTPR RMB 3
ISATOM RMB 3
GETC JMP GTCTTY
NUMINV RMB 3
NUMFRM RMB 3
FRMNUM RMB 3
PUTSYM RMB 3
PUTC JMP PTCTTY
ERRBRK RMB 3
GETSYM RMB 3
STKFRG RMB 3
GCOL RMB 3
PROPSH RMB 3
GETCAR RMB 3
ISVAR RMB 3
PRINR RMB 3
PROPOP RMB 3
*
 ORG OBEGAD
*
MAGWRD EQU $2100
*
DOSPCH EQU $AC18
DOSEOC EQU $AC02
DOSBPT EQU $AC14
INBUFF EQU $AD1B
NXTCH EQU $AD27
DOSFMS EQU $B406
DOSFCB EQU $A840
DOSTEX EQU $AD33
DOSRPT EQU $AD3F
DOSCWD EQU $AC0C
PCRLF EQU $AD24
PTCTTY EQU $AD18
*
GTCTTY LDAA DOSPCH
 CMPA DOSEOC
 BNE GETC2
 INC DOSBPT+1
 BNE GETC3
 INC DOSBPT
*
GETC3 JMP NXTCH
*
GETC2 CMPA #$D
 BNE GETC3
 JSR INBUFF
 JSR PCRLF
 CLR DOSPCH
 LDAA #$A
 RTS
*
LODFUN FDB MAGWRD
 LDX #DOSFCB
 STX FLP
 BSR GNXTA
 JSR OPENFL
 BCS LODRT
*
LODLUP JSR READ
 CPX EOIATM
 BEQ LODRT1
 JSR EVAL
 BRA LODLUP
*
LODRT1 JSR CLSFN1
LODRT LDX ZERO
 RTS
*
GNXTA LDX ALP
 JSR GNXTL
 STX ALP
 LDX XTMP
 RTS
*
OPNFUN FDB MAGWRD
 LDX #DOSFCB
*
OPNF2 STX FLP
 BSR GNXTA
 STX FORM
 BSR OPENFL
 BCS OPNFER
 LDX FORM
 RTS
*
OPNFER LDX ZERO
 RTS
*
OPOFUN FDB MAGWRD
 LDX #OUTFCB
 BRA OPNF2
*
CLOFUN FDB MAGWRD
*
CLOFN1 LDX #OUTFCB
 BRA CLSF2
*
CLSFUN FDB MAGWRD
*
CLSFN1 LDX #DOSFCB
*
CLSF2 STX FLP
 LDAA #4
 STAA 0,X
 JSR DOSFMS
 JSR OPNTTY
 BRA OPNFER
*
OPNERR LDX #OPNEMS
 JSR ERRBRK
*
OPENFL JSR ISATOM
 BCS OPNERR
 BLT OPNERR
*
 BEQ OPNTTY
 CPX EOIATM
 BEQ OPNTTY
 CPX TATOM
 BEQ OPNDSK
*
 LDAB #3
 STAB XTMP
 LDAB #8
 DEX
 LDX CAR,X
*
OPNLP1 LDAA 0,X
 BEQ OPNL1E
 INX
 CMPA #'"
 BEQ OPNLP1
 CMPA #'.
 BEQ OPNL1E
 TSTB
 BEQ OPNLP1
 PSHA
 DECB
 BRA OPNLP1
*
OPNL1E CLRA
 TSTB
 BEQ OPNL1R
*
OPNL2E PSHA
 DECB
 BNE OPNL2E
*
OPNL1R LDAB XTMP
 BEQ OPNL2R
 CLR XTMP
 BRA OPNLP1
*
OPNL2R LDX FLP
 LDAB #11
*
OPNLP2 PULA
 STAA 14,X
 DEX
 DECB
 BNE OPNLP2
*
 LDX FLP
 LDAA DOSCWD
 STAA 3,X
 LDAA #1
 JSR DOSTEX
 LDX FLP
 LDAA #1
 CPX #OUTFCB
 BNE OPNRED
 INCA
*
OPNRED STAA 0,X
 JSR DOSFMS
 BNE OPNOER
*
OPNDSK LDX FLP
 CPX #OUTFCB
 BEQ OPODSK
 LDX #GTCDSK
 STX GETC+1
*
OPNRET CLC
 RTS
*
OPODSK LDX #PTCDSK
 STX PUTC+1
 BRA OPNRET
*
OPNTTY LDX FLP
 CPX #OUTFCB
 BEQ OPOTTY
 LDX #GTCTTY
 STX GETC+1
 BRA OPNERT
*
OPOTTY LDX #PTCTTY
 STX PUTC+1
 BRA OPNERT
*
OPNOER JSR DOSRPT
*
OPNERT SEC
 RTS
*
RATFUN FDB MAGWRD
 JMP GETSYM
*
GNMFUN FDB MAGWRD
*
GNM1 LDX SPCPTR
 STX SYMPTR
 LDX GNMLAS
 BNE GNM2
 LDX #GNMFIR
 BRA GNM3
*
GNM2 DEX
 LDX CAR,X
*
GNM3 LDAA 0,X
 STX XTMP
 LDX SPCPTR
 CPX ENDPTR
 BPL GNMBAD
 STAA 0,X
 INX
 STX SPCPTR
 TSTA
 BEQ GNM4
 LDX XTMP
 INX
 BRA GNM3
*
GNM4 LDX SYMPTR
 INX
 INX
 INX
*
GNM41 LDAA 0,X
 BNE GNM42
 LDAA #'A
 STAA 0,X
 INX
 CPX ENDPTR
 BPL GNMBAD
 CLR 0,X
 INX
 STX SPCPTR
 BRA GNM51
*
GNM42 INCA
 CMPA #'Z
 BLS GNM5
 LDAA #'A
 STAA 0,X
 INX
 BRA GNM41
*
GNM5 STAA 0,X
*
GNM51 LDX SYMPTR
 JSR LOOKUP
 STX GNMLAS
 DEX
 LDX CAR,X
 CPX SYMPTR
 BNE GNM1
 LDX GNMLAS
 RTS
*
GNMBAD LDX #GNMEMS
 JMP ERREX
*
GNMLAS FDB 0
*
GNMFIR FCC 'TMP'
 FCB 0
*
GTCDSK STX XTMP
 LDX #DOSFCB
 STX FLP
 TST 0,X
 BNE GTCDE2
 LDAA 2,X
 CMPA #1
 BNE GTCDE2
 JSR DOSFMS
 BNE GTCDER
 BSR CASWIT
*
GTCDRT LDX XTMP
 RTS
*
GTCDER LDX FLP
 LDAA #4
 STAA 0,X
 JSR DOSFMS
 LDAA #4
 STAA GTCECT
*
GTCDE2 DEC GTCECT
 BGT GTCDE3
 JSR OPNTTY
*
GTCDE3 LDAA #EOI
 BRA GTCDRT
*
GTCECT FCB 0
*
CASWIT RTS
***
 ORG $1071
PTCDSK PSHB
 STX XTMP
 LDX #OUTFCB
 STX FLP
 TST 0,X
 BNE PTCDE2
 LDAB 2,X
 CMPB #2
 BNE PTCDE2
 BSR CASWIT
 JSR DOSFMS
 BNE PTCDER
*
PTCDRT LDX XTMP
 PULB
 RTS
*
PTCDER JSR DOSRPT
*
PTCDE2 JSR CLOFN1
 BRA PTCDRT
 LIB SUBLS2
BEGADR EQU *
 END

