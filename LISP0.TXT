 NAM LISP
 OPT NOG,PAG
 PAG
*SIMPLE LISP INTERPRETER
*
OBEGAD EQU $200
CAR EQU 0
CDR EQU 2
EOI EQU 4
 LIB LISPLB
*GLOBAL VECTORS
 ORG $0100
EVAL RMB 3
 JMP READ
PRINT RMB 3
 RMB 3
GETCEL RMB 3
FRECEL RMB 3
PUSHX RMB 3
POPX RMB 3
TOPX RMB 3
EVLATM RMB 3
SETATM RMB 3
LOOKUP RMB 3
GNXTL RMB 3
GFNXTL RMB 3
LSTINI RMB 3
LSTADD RMB 3
LSTAD2 RMB 3
LSTEND RMB 3
LSTEN0 EQU POPX
 JMP ERREX
ATMINI RMB 3
ISDTPR RMB 3
ISATOM RMB 3
GETC RMB 3
NUMINV RMB 3
NUMFRM RMB 3
FRMNUM RMB 3
PUTSYM RMB 3
PUTC RMB 3
 JMP ERRBRK
GETSYM RMB 3
STKFRG RMB 3
GCOL RMB 3
PROPSH RMB 3
 JMP GETCAR
ISVAR RMB 3
PRINR RMB 3
PROPOP RMB 3
*
 ORG OBEGAD
*
START EQU *
 STS SPSAVE
 LDX BEGPTR
 STX SPCPTR
 LDX ENDMEM
 STX ENDPTR
*
 LDAA ENDMEM
 STAA XTMP
 LDAA #$40
 LDAB #192
NUMILP EQU *
 STAA 0,X
 INX
 DECB
 BNE NUMILP
*
 LDX ENDMEM
 CLRA
NUM64I EQU *
 LSRA
 STAA 0,X
 INX
 ASLA
 ADDA #2
 DAA
 BCC NUM64I
*
 LDAA #$80
 STAA XTMP+1
 LDX XTMP
 LDAA #$40
NM128I EQU *
 LDAB #5
N128I2 EQU *
 STAA 0,X
 INX
 INCA
 DECB
 BNE N128I2
 INX
 INX
 INX
 CMPA #50+$40
 BNE NM128I
*
 CLRA
 STAA PEEKC
 LDX ZERO
 STX FREPTR
 STX SYMLST
 STX STKPTR
 STX NILATM
 STX CUREVL
 STX FORM
*
 JSR ATMINI
*
BIGLUP EQU *
 CLR RSTFLG
 LDX #PROMSG
 JSR PSTRNG
 JSR READ
 JSR PCRLF
 JSR EVAL
 BSR PPRINT
 BRA BIGLUP
*
ERREX EQU *
 LDS SPSAVE
 JSR PSTRNG
 JSR PCRLF
 SWI
 JMP START
*
PPRINT JMP PRINT
*
ERRBRK EQU *
 INC EFLAG
 JSR PSTRNG
 JSR PCRLF
 LDX FORM
 BEQ ERRBR2
 BSR PPRINT
ERRBR2 EQU *
 LDX CUREVL
 JSR GETCAR
 BCS ERRBR3
 BSR PPRINT
ERRBR3 LDAA RSTFLG
 BEQ ERRLUP
ERRSET LDX EOIATM
 ADDA #'0
 CMPA EFLAG
 BLO ERRETN
 CLR RSTFLG
ERRLUP LDX #EFLAG
 LDAA 0,X
 CMPA #'1
 BNE ERRLP2
 INX
ERRLP2 JSR PSTRNG
 JSR READ
 STX FORM
 JSR GETCAR
 CPX CONATM
 BEQ ERRCON
 LDX FORM
 JSR EVAL
 LDAA RSTFLG
 BNE ERRSET
 BSR PPRINT
 BRA ERRLUP
*
ERRCON LDX FORM
 LDX CDR,X
 JSR GETCAR
 JSR EVAL
ERRETN DEC EFLAG
 STX FORM
 RTS
*
EFLAG FCB '0
 FCC ':> '
 FCB 4
*
ISFORM STX XTMP
 BLE ISFRT
 CPX ENDPTR
 BPL ISFRT
 SEC
 RTS
ISFRT CLC
 RTS
*
READ CLR RBRKFG
READR JSR GETQSY
*
 CPX LPARAT
 BEQ READL
 CPX LBRKAT
 BNE READRT
 BSR READL
 CLR RBRKFG
READRT RTS
*
RBRKFG FCB 0
*
READL JSR LSTINI
 JSR STKFRG
RDL2 BSR READR
 CPX RPARAT
 BEQ RDLRT
 CPX DOTATM
 BEQ RDLDOT
*
 CPX EOIATM
 BEQ RDLEOI
 JSR LSTAD2
 BRA RDL2
*
RDLEOI INC RBRKFG
RDLRT JSR LSTEN0
RDLRT2 JSR PROPOP
 STX FORM
 RTS
*
RDLDOT BSR READR
 JSR LSTEND
 JSR GETQSY
 CPX RPARAT
 BEQ RDLRT2
*
RLSTER JSR PROPOP
 STX FORM
 LDX #RLERMS
 DEC RSTFLG
 JSR ERRBRK
 CLR RSTFLG
 RTS
*
GETQSY LDAA RBRKFG
 BNE GQSRPR
 JSR GETSYM
 CPX RBRKAT
 BEQ GQSRBK
 CPX SQUTAT
 BNE GQSRT
 JSR READ
 STX FORM
 JSR GETCEL
 LDAA FORM
 STAA CAR,X
 LDAA FORM+1
 STAA CAR+1,X
 STX FORM
 JSR GETCEL
 LDAA QUOATM
 STAA CAR,X
 LDAA QUOATM+1
 STAA CAR+1,X
 LDAA FORM
 STAA CDR,X
 LDAA FORM+1
 STAA CDR+1,X
GQSR0 STX FORM
GQSRT RTS
*
GQSRBK INC RBRKFG
GQSRPR LDX RPARAT
 BRA GQSR0
*
GETCAR STX XTMP
 BLE GCANIL
 ROR XTMP+1
 BCS GCANIL
 LDX CAR,X
 RTS
GCANIL LDX ZERO
 SEC
 RTS
*
PSTRNG EQU $AD1E
PCRLF EQU $AD24
*
RLERMS FCC 'READ LIST ERROR'
 FCB 4
PROMSG FCC '-> '
 FCB 4
BEGADR EQU *
 END START

