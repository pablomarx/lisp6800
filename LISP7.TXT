 NAM LISP-SUBRS3
 OPT PAG
 PAG
OBEGAD EQU $11E0
CAR EQU 0
CDR EQU 2
EOI EQU 4
*
 LIB LISPLB7
* GLOBAL VECTORS
 ORG $0100
EVAL RMB 3
READ RMB 3
PRINT RMB 3
 RMB 3
GETCEL RMB 3
FRECEL RMB 3
PUSHX RMB 3
POPX RMB 3
TOPX RMB 3
EVLATM RMB 3
SETATM RMB 3
LOOKUP RMB 3
GNXTL RMB 3
GFNXTL RMB 3
LSTINI RMB 3
LSTADD RMB 3
LSTAD2 RMB 3
LSTEND RMB 3
LSTEN0 EQU POPX
ERREX RMB 3
ATMINI RMB 3
ISDTPR RMB 3
ISATOM RMB 3
GETC RMB 3
 JMP NUMINV
NUMFRM RMB 3
FRMNUM RMB 3
PUTSYM RMB 3
PUTC RMB 3
ERRBRK RMB 3
GETSYM RMB 3
STKFRG RMB 3
GCOL RMB 3
PROPSH RMB 3
GETCAR RMB 3
ISVAR RMB 3
PRINR RMB 3
PROPOP RMB 3
*
 ORG OBEGAD
*
MAGWRD EQU $2100
*
ATMFUN FDB MAGWRD
 BSR GNXTA
 JSR ISATOM
 BCS FALSE
*
TRUE LDX TATOM
 RTS
*
FALSE LDX ZERO
 RTS
*
NUMFUN FDB MAGWRD
 BSR GNXTA
 JSR ISATOM
 BCS FALSE
 BGE FALSE
 BRA TRUE
*
ADDFUN FDB MAGWRD
 BSR GNXTNM
 STX FLP
 BSR GNXTNM
*
ADDF2 STX NLP
 LDAA FLP+1
 ADDA NLP+1
 DAA
 STAA FLP+1
 LDAA FLP
 ADCA NLP
 DAA
 STAA FLP
 LDX FLP
 JMP NUMFRM
*
SUBFUN FDB MAGWRD
*
SUBFN1 BSR GNXTNM
 STX FLP
 BSR GNXTNM
 JSR NUMINV
 BRA ADDF2
*
GTRFUN FDB MAGWRD
 BSR SUBFN1
 JSR FRMNUM
 BLE FALSE
 BRA TRUE
*
GNXTNM LDX ALP
 JSR GNXTL
 STX ALP
 LDX XTMP
*
GNN2 JSR ISATOM
 BCS GNNERR
 BGE GNNERR
 JMP FRMNUM
*
GNNERR STX FORM
 LDX #GNNEMS
 JSR ERRBRK
 LDAA RSTFLG
 BEQ GNN2
 LDX ZERO
 RTS
*
GNXTA LDX ALP
 JSR GNXTL
 STX ALP
 LDX XTMP
 RTS
*
CNWFUN FDB MAGWRD
 JSR LSTINI
 BSR GNXTA
 BCS CNWDN2
 STX FLP
*
CNWEVL JSR EVAL
 JSR ISDTPR
 BEQ CNWNXT
 BCS CNWDUN
 JSR LSTCON
 BCS CNWDN2
*
CNWNXT LDX FLP
 BRA CNWEVL
*
CNWDUN CPX EOIATM
 BEQ CNWDN2
 JSR LSTEND
 BRA CNWDN3
*
CNWDN2 JSR LSTEN0
*
CNWDN3 JMP PROPOP
*
LSTFUN FDB MAGWRD
 LDX STKPTR
 CLR CAR,X
 CLR CAR+1,X
 LDX ALP
 RTS
*
LSTCON STX FORM
 JSR TOPX
 LDAA FORM
 STAA CDR,X
 LDAA FORM+1
 STAA CDR+1,X
*
LSTC2 STX XTMP2
 LDX CDR,X
 JSR ISDTPR
 BCC LSTC2
 BNE LSTC3
 CLC
*
LSTC3 LDX STKPTR
 LDAA XTMP2
 STAA CAR,X
 LDAA XTMP2+1
 STAA CAR+1,X
 LDX FORM
 RTS
*
EVLSFN FDB MAGWRD
 LDX ALP
 JSR ISDTPR
 BCS EVLLS2
 LDX CAR,X
 BRA EVLLS2
*
PGNFUN FDB MAGWRD
 LDX ALP
*
EVLLS2 STX FLP
*
PGNFN2 JSR ISDTPR
 BCS PGNDUN
 STX ALP
 LDX CAR,X
 JSR EVAL
 STX FLP
 LDX ALP
 LDX CDR,X
 BRA PGNFN2
*
PGNDUN LDX FLP
 RTS
*
NUMINV STX XTMP
 BEQ NMIVRT
 LDAA #$99
 TAB
 SUBB XTMP
 SUBA XTMP+1
 ADDA #1
 DAA
 STAA XTMP+1
 TBA
 ADCA #0
 DAA
 STAA XTMP
 LDX XTMP
*
NMIVRT RTS
*
RSTFUN FDB MAGWRD
*
RSTFN2 LDAA #-1
*
RSTFN3 STAA RSTFLG
*
RTNEOI LDX EOIATM
*
BTFRTN RTS
*
RTBFUN FDB MAGWRD
 JSR GNXTA
 JSR ISATOM
 BCS RTBFN2
 BGE RTBFN2
 JSR FRMNUM
 BLE RSTFN2
 STX XTMP
 LDAA XTMP+1
 BRA RSTFN3
*
RTBFN2 LDAA #$FF-'0
 BRA RSTFN3
*
BRKFUN FDB MAGWRD
 LDX ZERO
 STX FORM
 LDX #BRKMSG
 JMP ERRBRK
*
BTFUN FDB MAGWRD
 CLR FLP+1
 JSR GNXTA
 JSR ISATOM
 BCS BTFUN1
 BGE BTFUN1
 JSR FRMNUM
 STX FLP
*
BTFUN1 LDX CUREVL
 STX ALP
*
BTFUN2 JSR GNXTA
 BCS RTNEOI
 LDAA FLP+1
 ADDA #$99
 DAA
 STAA FLP+1
 BEQ BTFRTN
 JSR GETCAR
 JSR PRINT
 BRA BTFUN2
*
SUBLS3 FCC 'ATOM'
 FCB 0
 FDB ATMFUN
 FCC 'NUMBER'
 FCB 0
 FDB NUMFUN
 FCC 'ADD'
 FCB 0
 FDB ADDFUN
 FCC 'SUB'
 FCB 0
 FDB SUBFUN
 FCC 'GREATER'
 FCB 0
 FDB GTRFUN
 FCC 'LIST'
 FCB 0
 FDB LSTFUN
 FCC 'EVALLIST'
 FCB 0
 FDB EVLSFN
 FCC 'RETBRK'
 FCB 0
 FDB RTBFUN
 FCC 'BT'
 FCB 0
 FDB BTFUN
*
 FCB 0
NSUBL3 FCC 'PROGN'
 FCB 0
 FDB PGNFUN
 FCC 'CONCWHILE'
 FCB 0
 FDB CNWFUN
 FCC 'RESET'
 FCB 0
 FDB RSTFUN
 FCC 'BREAK'
 FCB 0
 FDB BRKFUN
*
 FCB 0
GNNEMS FCC 'NUMBER REQUIRED'
 FCB 4
BRKMSG FCC 'BREAKING...'
 FCB 4
*
BEGADR EQU *
 END

